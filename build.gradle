plugins {
	id "dev.architectury.loom" version "0.7.2-SNAPSHOT"
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.github.johnrengelman.shadow" version "7.0.0"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

configurations {
	shade
}

loom {
	silentMojangMappingsLicense()

	mixinConfigs = [
	]

	useFabricMixin = true

	dataGen {
		mod project.mod_id
	}

	launches {
		data {
			arg "--existing", file("src/main/resources").absolutePath
		}
	}
}

repositories {
	maven {
		url "https://maven.shedaniel.me/"
		content {
			includeGroup "me.shedaniel"
		}
	}

	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.ftb.mods"
		}
	}

	maven {
		url "https://maven.tterrag.com/"
		content {
			includeGroup "com.tterrag.registrate"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings minecraft.officialMojangMappings()
	forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
	modImplementation "curse.maven:jei-238222:${jei_file}"
	modImplementation "me.shedaniel:architectury-forge:${project.architectury_version}"
	modImplementation "dev.latvian.mods:kubejs-forge:${project.kubejs_version}"
	modImplementation "dev.latvian.mods:rhino:${rhino_version}"
	modImplementation "dev.ftb.mods:ftb-library-forge:${rootProject.ftb_library_version}"
	modImplementation "curse.maven:bookshelf-228525:${bookshelf_file}"
	modImplementation "curse.maven:game-stages-268655:${gamestages_file}"

	def registrate = "com.tterrag.registrate:Registrate:MC1.16.5-1.0.10"
	modImplementation(registrate)
	shade(registrate)

}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

shadowJar {
	configurations = [project.configurations.shade]
	relocate 'com.tterrag.registrate', 'dev.ftb.mods.ftbstructures.repack.registrate'
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}

	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

remapJar {
	dependsOn(shadowJar)
	input.set(shadowJar.archiveFile)
}

/*
remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.mod_name}-${project.name}"
	archiveClassifier.set null
}
*/

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			artifact(remapJar) {
				classifier null
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "ftb"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = "release"
			addGameVersion "1.16.5"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'ftb-library-forge'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}
